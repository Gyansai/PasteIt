apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
def signingProperties = file('../PasteIt.signing.properties')
def increaseVersionCode() {
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def code = versionProps['VERSION_CODE'].toInteger() + 1
        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
        return code
    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'
    if (signingProperties.canRead()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file(signingProperties)))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    }
    defaultConfig {
        applicationId "org.teamblueridge.pasteitapp"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode increaseVersionCode()
        versionName "6.0.0." + versionCode
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
        beta {
            versionNameSuffix '-beta'
            signingConfig signingConfigs.release
        }
        alpha {
            versionNameSuffix '-alpha'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.pawegio.kandroid:kandroid:0.3.1@aar'

}
buildscript {
    ext.kotlin_version = '1.0.0-beta-1103'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}
repositories {
    mavenCentral()
    jcenter()
}
