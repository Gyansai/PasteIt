apply plugin: 'com.android.application'
def signingProperties = file('../PasteIt.signing.properties')
def increaseVersionCode() {
    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
        return code

    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"
    if (signingProperties.canRead()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file(signingProperties)))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    }
    defaultConfig {
        useJack true
        applicationId "org.teamblueridge.pasteitapp"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode increaseVersionCode()
        versionName "5.1.2." + increaseVersionCode()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
        beta {
            versionNameSuffix '-beta'
            signingConfig signingConfigs.release
        }
        alpha {
            versionNameSuffix '-alpha'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.+'
}